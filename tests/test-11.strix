// Test clicking on canvas... with tic-tac-toe
string board = "---------";
// string board = "--XOXOX--";
int mouse_id = -1;
int turn = 'X';
int win_id = 0; // (1-3 rows, 4-6 cols, 7-8 diagonals)
int move_count = 0;

function TestWin() : int {
  if (move_count < 5) return 0; // Fifth move is the first possibility of winning.

  // Using '*' to simulate '&&'
  if ((board[0] != '-') * (board[0] == board[1]) * (board[1] == board[2])) win_id = 1;
  else if ((board[3] != '-') * (board[3] == board[4]) * (board[4] == board[5])) win_id = 2;
  else if ((board[6] != '-') * (board[6] == board[7]) * (board[7] == board[8])) win_id = 3;
  else if ((board[0] != '-') * (board[0] == board[3]) * (board[3] == board[6])) win_id = 4;
  else if ((board[1] != '-') * (board[1] == board[4]) * (board[4] == board[7])) win_id = 5;
  else if ((board[2] != '-') * (board[2] == board[5]) * (board[5] == board[8])) win_id = 6;
  else if ((board[0] != '-') * (board[0] == board[4]) * (board[4] == board[8])) win_id = 7;
  else if ((board[2] != '-') * (board[2] == board[4]) * (board[4] == board[6])) win_id = 8;
  else if (move_count == 9) win_id = 9;

  return win_id;
}

function DrawBoard() : int {
  // Draw Background
  LineColor("black");
  FillColor("White");
  LineWidth(6);
  Rect(0,0,600,600);

  // Draw grid lines
  Line(200, 0, 200, 600);
  Line(400, 0, 400, 600);
  Line(0, 200, 600, 200);
  Line(0, 400, 600, 400);

  int row = 0;
  while (row < 3) {
    int col = 0;
    while (col < 3) {
      int id = row*3 + col;
      if (board[id] == 'X') {
        LineColor("#600000");
        LineWidth(12);
        Line(col*200+20, row*200+20, col*200+180, row*200+180);
        Line(col*200+180, row*200+20, col*200+20, row*200+180);
      }
      else if (board[id] == 'O') {
        LineColor("#000060");
        LineWidth(12);
        FillColor("white");
        Circle(col*200+100, row*200+100, 80);
      }
      else if (id == mouse_id) {
        LineColor("#C0C0C0");
        LineWidth(12);
        if (turn == 'X') {
          Line(col*200+20, row*200+20, col*200+180, row*200+180);
          Line(col*200+180, row*200+20, col*200+20, row*200+180);
        } else {
          Circle(col*200+100, row*200+100, 80);
        }
      }
      col = col + 1;
    }
    row = row + 1;
  }

  if (win_id) {
    LineColor("#006000");
    LineWidth(8);

    int win_char = '-';
    if (win_id == 1)      { Line(20, 100, 580, 100); win_char = board[0]; }
    else if (win_id == 2) { Line(20, 300, 580, 300); win_char = board[3]; }
    else if (win_id == 3) { Line(20, 500, 580, 500); win_char = board[6]; }
    else if (win_id == 4) { Line(100, 20, 100, 580); win_char = board[0]; }
    else if (win_id == 5) { Line(300, 20, 300, 580); win_char = board[1]; }
    else if (win_id == 6) { Line(500, 20, 500, 580); win_char = board[2]; }
    else if (win_id == 7) { Line(20,  20, 580, 580); win_char = board[0]; }
    else if (win_id == 8) { Line(20, 580, 580, 20);  win_char = board[2]; }

    string win_label = "Winner: " + win_char;
    if (win_id == 9) {
      win_label = "Tie game!";
    }

    FillColor("black");
    LineColor("white");
    LineWidth(5);
    Rect(180, 0, 240, 60);
    FillColor("yellow");
    Text(190, 10, 50, win_label);
  }

  return 0;
}

function MouseClick(int x, int y) : int {
  if (win_id) return 1; // Game is already over!
  int col = x / 200;
  int row = y / 200;
  mouse_id = row*3 + col;
  if (board[mouse_id] == '-') {
    if (turn == 'X') {
      board[mouse_id] = 'X';
      turn = 'O';
    } else {
      board[mouse_id] = 'O';
      turn = 'X';
    }
    move_count = move_count + 1;
    TestWin();
  }
  DrawBoard();
  return 0;
}

function MouseMove(int x, int y) : int {
  int col = x / 200;
  int row = y / 200;
  mouse_id = row*3 + col;
  DrawBoard();
  return 0;
}

function Main() : int {
  SetTitle("Test: Canvas clicks with tic-tac-toe");
  DrawBoard();
  AddClickFun("MouseClick");
  AddMoveFun("MouseMove");
  return 0;
}
